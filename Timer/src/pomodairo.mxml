<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication
    xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns:mx="library://ns.adobe.com/flex/mx"
    xmlns:ns1="com.pomodairo.components.*"
    showFlexChrome="false" layout="absolute"
    verticalScrollPolicy="off" horizontalScrollPolicy="off"
    height="200" width="320"
    backgroundColor="#292929"
    showStatusBar="false"
    title="{display.appTitle}"
    initialize="initializeHandler()"
    creationComplete="creationComplete()"
    closing="beforeClose()"
    windowResize="resizeHandler(event)"
    close="shutdown()">


    <fx:Script>
		<![CDATA[
        import air.update.ApplicationUpdaterUI;
        import air.update.events.UpdateEvent;

        import com.pomodairo.EmbedImages;
        import com.pomodairo.EmbedStyle;
        import com.pomodairo.components.config.BaseConfigPanel;
        import com.pomodairo.components.config.SettingsPanel;
        import com.pomodairo.components.popup.ConfirmPanel;
        import com.pomodairo.components.popup.PomoItemInput;
        import com.pomodairo.core.PomodoroEventDispatcher;
        import com.pomodairo.core.Storage;
        import com.pomodairo.core.TaskManager;
        import com.pomodairo.core.TimeManager;
        import com.pomodairo.data.Pomodoro;
        import com.pomodairo.events.ConfigurationUpdatedEvent;
        import com.pomodairo.events.GuiChangeEvent;
        import com.pomodairo.events.PomodoroEvent;
        import com.pomodairo.settings.ConfigItemName;
        import com.pomodairo.settings.ConfigManager;
        import com.pomodairo.settings.DefaultConfig;
        import com.pomodairo.settings.DefaultConfig;
        import com.pomodairo.twitter.TwitterManager;

        import mx.core.IFlexDisplayObject;
        import mx.events.MenuEvent;
        import mx.managers.PopUpManager;

        private var appUpdater:ApplicationUpdaterUI = new ApplicationUpdaterUI();

        private var taskManager:TaskManager;
        private var timeManager:TimeManager;
        private var twitterManager:TwitterManager;

        private var eventDispatcher:PomodoroEventDispatcher;
        private var storage:Storage;
        private var config:ConfigManager;

        private function initializeHandler():void {

            trace("[INFO][pomodairo] initialized");

            // 1. Create global event dispatcher
            eventDispatcher = PomodoroEventDispatcher.instance;

            // Create config
            config = ConfigManager.instance;
            // Inject config
            config.defaultSettingsFilePath = DefaultConfig.DEFAULT_SETTINGS_FILE_PATH;
            // Initialize config
            config.initialize();

            //------------------------------------------------------------------
            // Create core managers
            //------------------------------------------------------------------

            // 2. Create and initialize data storage.
            storage = Storage.instance;

            // 5. Primary
            taskManager = TaskManager.instance;
            timeManager = new TimeManager();

            //------------------------------------------------------------------
            // Injection
            //------------------------------------------------------------------

            config.dbProvider = storage;

            storage.configManager = config;

            timeManager.eventDispatcher = eventDispatcher;
            timeManager.config = config;

            taskManager.eventDispatcher = eventDispatcher;
            taskManager.configManager = config;
            taskManager.storage = storage;

            // UI
            display.timer = timeManager;
            display.taskManager = taskManager;
            display.eventDispatcher = eventDispatcher;

            listContainer.config = config;
            listContainer.storage = storage;
            listContainer.timeManager = timeManager;
            listContainer.taskManager = taskManager;
            listContainer.eventDispatcher = eventDispatcher;

            //------------------------------------------------------------------
            // Initialize
            //------------------------------------------------------------------

            // 3. Init and open storage
            storage.initialize();
            storage.initViews();

            timeManager.initialize();

            taskManager.initialize();

            //UI
            display.init();
            listContainer.init();


            //------------------------------------------------------------------
            // Initialize
            //------------------------------------------------------------------

            eventDispatcher.addEventListener(ConfigurationUpdatedEvent.UPDATED, onConfigurationChange);

            var remember:Boolean = ConfigManager.instance.getBoolean(ConfigItemName.REMEMBER_POSITION);
            if (remember) {
                var setX:Number = ConfigManager.instance.getFloat(ConfigItemName.WINDOW_X);
                var setY:Number = ConfigManager.instance.getFloat(ConfigItemName.WINDOW_Y);
                if (setX >= 0 && setX < Capabilities.screenResolutionX) {
                    this.nativeWindow.x = setX;
                }
                if (setY >= 0 && setY < Capabilities.screenResolutionY) {
                    this.nativeWindow.y = setY;
                }
            }

            var setHeight:Number = ConfigManager.instance.getFloat(ConfigItemName.WINDOW_HEIGHT);
            if (setHeight >= DefaultConfig.MINI_HEIGHT && setHeight < Capabilities.screenResolutionY) {
                this.height = setHeight;
            }

            var setWidth:Number = ConfigManager.instance.getFloat(ConfigItemName.WINDOW_WIDTH);
            if (setWidth >= DefaultConfig.MIN_WIDTH && setHeight < Capabilities.screenResolutionX) {
                this.width = setWidth;
            }

            dialogList[CONFIG_DIALOG_ID] = new SettingsPanel();
            dialogList[CONFIRM_DIALOG_ID] = new ConfirmPanel();

            EmbedStyle.registerFonts();
        }

        private function creationComplete():void {
            checkForUpdates();
            eventDispatcher.addEventListener(PomodoroEvent.TIME_OUT, onPomodoroTimeout);
            eventDispatcher.addEventListener(GuiChangeEvent.UPDATED, onGuiChange);

            //Playback buttons
            eventDispatcher.addEventListener(PomodoroEvent.SELECTED, playbackEvents);
            eventDispatcher.addEventListener(PomodoroEvent.NEXT_TASK_SELECTED, playbackEvents);
            eventDispatcher.addEventListener(PomodoroEvent.START_BREAK, playbackEvents);
            eventDispatcher.addEventListener(PomodoroEvent.START_POMODORO, playbackEvents);
            eventDispatcher.addEventListener(PomodoroEvent.STOP_POMODORO, playbackEvents);

            // Display task list on startup
            toggleTaskList();

            taskManager.findNextTask();
        }

        private function playbackEvents(event:PomodoroEvent):void {
            switch (event.type) {
                case PomodoroEvent.SELECTED:
                case PomodoroEvent.NEXT_TASK_SELECTED:
                    startTimerButton.enabled = true;
                    stopBreakButton.enabled = true;
                    if (!timeManager.isRunning) {
                        captionLabel.text = event.pomodoro.shortDescription;
                    }
                    break;

                case PomodoroEvent.START_BREAK:
                    startTimerButton.enabled = true;
                    stopBreakButton.enabled = true;
                    break;

                case PomodoroEvent.START_POMODORO:
                    taskListVisibleOnStart = taskList.visible;
                    closeTaskList();
                    startTimerButton.enabled = false;
                    stopBreakButton.enabled = true;
                    break;

                case PomodoroEvent.STOP_POMODORO:
                    startTimerButton.enabled = true;
                    stopBreakButton.enabled = false;
                    if (taskListVisibleOnStart) {
                        toggleTaskList(taskListLastTab);
                    }
                    break;
            }
        }

        private function checkForUpdates():void {
            appUpdater.configurationFile = new File("app:/updateConfig.xml");
            appUpdater.isCheckForUpdateVisible = false;
            appUpdater.addEventListener(UpdateEvent.INITIALIZED, updaterInitialised);
            appUpdater.initialize();
        }

        private function updaterInitialised(event:UpdateEvent):void {
            appUpdater.checkNow();
        }

        private function onConfigurationChange(e:ConfigurationUpdatedEvent):void {
            trace("Configuration change: " + e.configElement.name + " -> " + e.configElement.value);
            if (e.configElement.name == ConfigItemName.ALWAYS_ON_TOP) {
                this.alwaysInFront = e.configElement.value == "true";
            }
        }

        private function shutdown():void {
//            twitterManager.stop();
        }

        private function onPomodoroTimeout(event:PomodoroEvent):void {
            nativeWindow.notifyUser(NotificationType.CRITICAL);
            nativeWindow.orderToFront();
        }

        private var taskListVisibleOnStart:Boolean = false;
        private var taskListLastTab:int = 0;

        private function toggleTaskList(tabIndex:int = 0):void {

            if (listContainer.visible && listContainer.selectedTab == tabIndex) {
                closeTaskList();
            } else if (listContainer.visible && listContainer.selectedTab != tabIndex) {
                listContainer.showSelectedPanel(tabIndex);
                taskListLastTab = tabIndex;
            } else if (!listContainer.visible) {
                listContainer.showSelectedPanel(tabIndex);
                listContainer.visible = true;
                taskListLastTab = tabIndex;
                var setHeight:Number = ConfigManager.instance.getFloat(ConfigItemName.WINDOW_HEIGHT);
                setHeight = isNaN(setHeight) ? 0 : setHeight;

                this.height = setHeight > Capabilities.screenResolutionY || setHeight < DefaultConfig.FULL_HEIGHT ? DefaultConfig.FULL_HEIGHT : setHeight;
            }
        }

        private function closeTaskList(event:PomodoroEvent = null):void {
            this.height = DefaultConfig.SHORT_HEIGHT;
            listContainer.visible = false;
        }

        private function beforeClose():void {
            Storage.instance.updateConfig("window.x", "" + this.nativeWindow.x);
            Storage.instance.updateConfig("window.y", "" + this.nativeWindow.y);
            Storage.instance.updateConfig("window.height", "" + this.height);
            Storage.instance.updateConfig("window.width", "" + this.width);
        }

        private function onGuiChange(event:GuiChangeEvent):void {
            if (event.miniView) {
                trace("Switch application to mini view");
                closeTaskList();
                this.height = DefaultConfig.MINI_HEIGHT;
            } else {
                trace("Switch application to regular view");
                this.height = DefaultConfig.SHORT_HEIGHT;
            }
        }

        private function toggleViewState():void {
            display.toggleMiniView();
        }

        private function onAddPomodoroClicked(event:Event = null):void {
            var input:PomoItemInput = new PomoItemInput();
            input.type = Pomodoro.TYPE_POMODORO;
            input.selectAfterCreate = true;
            PopUpManager.addPopUp(input, this, true);
            PopUpManager.centerPopUp(input);
        }

        private function resizeHandler(event:Event):void {
            if (this.height == DefaultConfig.MINI_HEIGHT || this.height == DefaultConfig.SHORT_HEIGHT)
                return;

            var value:Number = ConfigManager.instance.getFloat(ConfigItemName.WINDOW_HEIGHT);
            if (this.height == value)
                return;

            Storage.instance.updateConfig('window.height', '' + this.height);
        }

        public static const STATISTIC_DIALOG_ID:String = "statistic";
        public static const CONFIG_DIALOG_ID:String = "config";
        public static const CONFIRM_DIALOG_ID:String = "confirm";

        private var dialogList:Object = {};

        private function menuClick(event:MenuEvent):void {
            var action:String = event.item.@action;
            switch (action) {
                case STATISTIC_DIALOG_ID:
//                    toggleStatisticList();
                    break;

                case CONFIG_DIALOG_ID:
                case CONFIRM_DIALOG_ID:
                    var dialog:IFlexDisplayObject = dialogList[action];
                    showPopup(dialog);
                    break;

                default:
                    trace("WARNING: Dialog with unhandled menu action \"" + action + "\"");
                    break;
            }
        }

        private function showSettingsPanel():void {
            var dialog:IFlexDisplayObject = dialogList[CONFIG_DIALOG_ID];
            dialog.width = stage.stageWidth;
            dialog.height = stage.stageHeight;
            showPopup(dialog);
        }

        private function showPopup(dialog:IFlexDisplayObject):void {
            if (dialog) {

                if (dialog is BaseConfigPanel) {
                    var panel:BaseConfigPanel = dialog as BaseConfigPanel;
                    panel.populate();
                    panel.notifyConfiguration();
                }
                PopUpManager.addPopUp(dialog, this, true);
                PopUpManager.centerPopUp(dialog);
                PopUpManager.bringToFront(dialog);
            }
        }

        private function lblClose_clickHandler(event:MouseEvent):void {
            if (config.getBoolean(ConfigItemName.CONFIRM_APPLICATION_EXIT) == false) {
                close();
                return;
            }

            if (display.currentState == "minimal") {
                display.toggleMiniView();
            }

            showPopup(dialogList[CONFIRM_DIALOG_ID]);
        }
        ]]>
    </fx:Script>


    <fx:Style source="/../../Pomodairo/src/style/style.css"/>

    <fx:Style>
        @font-face
        {
            font-family: Digital;
            src: url("/../../Pomodairo/src/assets/digital-7mono.ttf");
            embedAsCFF: false;
        }
        .MiniClockText
        {
            font-family: Digital;
            font-size: 28pt;
        }

    </fx:Style>

    <s:VGroup left="0" top="0" height="{DefaultConfig.SHORT_HEIGHT}" right="0"
            mouseDown="{nativeWindow.startMove()}">

        <!-- TopPanel -->
        <s:HGroup width="100%" height="32" gap="2">

            <mx:Button id="settingsButton"
                       width="32" height="32"
                       icon="{EmbedImages.ICON_ADJUST_CLASS}"
                       click="{showSettingsPanel()}"/>

            <mx:Spacer width="100%"/>

            <mx:Button right="56" top="0" height="32" id="toggleViewButton"
                    label="-" click="toggleViewState()"/>
            <mx:Button label="_" width="32" height="32" click="{minimize()}"
                    color="#FFFFFF"/>
            <mx:Button label="x" width="32" height="32"
                    click="{lblClose_clickHandler(event)}" color="#FFFFFF"/>
        </s:HGroup>

        <ns1:Display id="display" height="64" width="100%"/>

        <s:HGroup width="100%" height="32" gap="2" verticalAlign="middle">

            <mx:Button id="startTimerButton" width="32" height="32"
                       click="{timeManager.startTimer(taskManager.activeTask)}"
                       icon="{EmbedImages.ICON_PLAY_CLASS}"/>
            <mx:Button id="stopBreakButton" width="32" height="32"
                       click="timeManager.stop();"
                       icon="{EmbedImages.ICON_STOP_CLASS}"/>
            <mx:Button id="nextTaskButton"  height="32"
                       click="display.onNextTaskClicked()"
                       icon="{EmbedImages.ICON_FORWARD_CLASS}"/>

            <mx:Label id="captionLabel"
                     styleName="MiniClockText"
                    color="{display.captionColor}"
                    width="230" fontFamily="Digital" fontSize="14"/>

            <mx:Button id="taskList"
                       width="32" height="32"
                       icon="{EmbedImages.ICON_LIST_CLASS}"
                       click="toggleTaskList()"
                       toolTip="Open/close task list"/>
            <mx:Button id="historyList"
                       width="32" height="32"
                       icon="{EmbedImages.ICON_HISTORY_CLASS}"
                       click="toggleTaskList(1)"
                       toolTip="Open/close task list"/>
        </s:HGroup>

    </s:VGroup>

    <ns1:PomodoroListContainer  id="listContainer"
        x="0" y="{DefaultConfig.SHORT_HEIGHT}" height="100%" width="100%"
        visible="false" showEffect="fadein" hideEffect="fadeout"
        backgroundAlpha="0"/>

    <ns1:ResizeLine bottom="0" left="0" right="0" height="4"
        backgroundColor="0xFFFFFF" backgroundAlpha="0.2"
        app="{this}"/>

</mx:WindowedApplication>
