<?xml version="1.0"?>
<s:ItemRenderer
    xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    height="30"
    rollOver="{hovered = true}"
    rollOut="{hovered = false}">


    <fx:Script>
        <![CDATA[
        import com.pomodairo.Pomodoro;

        private var _pomodoro:Pomodoro = null;
        private var _strikethrough:Boolean = false;

        override public function set data(value:Object):void {
            super.data = value;
            _pomodoro = data as Pomodoro;
            if (_pomodoro == null) {
                nameLabel.text = "";
                estimationLabel.text = "";
                actualLabel.text = "";
                unplanedLabel.text = "";
                interaptionsLabel.text = "";
            } else {
                nameLabel.text = _pomodoro.name;
                estimationLabel.text = _pomodoro.estimated.toString();
                actualLabel.text = _pomodoro.pomodoros.toString();
                unplanedLabel.text = _pomodoro.unplanned.toString();
                interaptionsLabel.text = _pomodoro.interruptions.toString();
            }
        }

        public function set strikethrough(bool:Boolean):void {
            this._strikethrough = bool;
            this.invalidateDisplayList();
        }

        public function get strikethrough():Boolean {
            return this._strikethrough;
        }

        override protected function updateDisplayList(unscaledWidth:Number,
                                                      unscaledHeight:Number):void {
            if (data.done) {
                //This row is selected update stuff
                strikethrough = true;
            }
            else {
                //This row is not selected reset stuff
                strikethrough = false;
            }

            this.setStyle('color', 0xCCCCCC);
            this.setStyle('fontStyle', 'normal');
            this.setStyle('fontWeight', 'normal');

            super.updateDisplayList(unscaledWidth, unscaledHeight);

            this.graphics.clear();
            if (this._strikethrough) {
                this.graphics.lineStyle(1, this.getStyle("color"));
                this.graphics.moveTo(0, unscaledHeight / 2);
                this.graphics.lineTo(unscaledWidth, unscaledHeight / 2);
            }
            else {
                if (data.estimated == 0) {
                    this.setStyle('color', 0x777777);
                }
                if (data.estimated < data.pomodoros) {
                    this.setStyle('color', 0xCC0000);
                }
                if (data.created.day != new Date().day) {
                    this.setStyle('fontStyle', 'italic');
                }
                if (data.pomodoros > 0) {
                    this.setStyle('fontWeight', 'bold');
                }
            }
        }
        ]]>
    </fx:Script>

    <s:states>
        <s:State name="normal"/>
        <s:State name="hovered"/>
    </s:states>

    <s:layout>
        <s:HorizontalLayout verticalAlign="middle" />
    </s:layout>

    <!--<s:Rect id="hover" top="0" right="0" left="0" bottom="0" visible="false">-->
        <!--<s:fill>-->
            <!--<s:SolidColor color="#7e7e7e" />-->
        <!--</s:fill>-->
    <!--</s:Rect>-->

    <s:Label id="nameLabel" width="100%" />
    <s:Label id="estimationLabel" width="28" />
    <s:Label id="actualLabel" width="28" />
    <s:Label id="unplanedLabel" width="28" />
    <s:Label id="interaptionsLabel" width="28" />


</s:ItemRenderer>
